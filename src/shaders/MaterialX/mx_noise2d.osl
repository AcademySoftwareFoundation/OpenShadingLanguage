/////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2016 Sony Pictures Imageworks Inc., et al.
// All Rights Reserved.
// BSD 3-clause license:
// https://github.com/imageworks/OpenShadingLanguage/License.md
/////////////////////////////////////////////////////////////////////////////


#include "oslutil.h"



shader
mx_noise2d
    [[ string help = "2D noise"  ]]
  (
    vector2 position = { u, v }
        [[  string help = "input coordinate position",
            int lockgeom = 0 ]],

    // FIXME: MatrialX says no scale on the 3D noise. Why not?
    float scale = 1
        [[  string help = "scaling factor for the noise function coordinate"  ]],
    float amplitude = 1
        [[  string help = "center-to-peak amplitude of the noise" ]],
    float pivot = 0
        [[  string help = "the center value of the output noise" ]],
    string noisetype = "snoise"
        [[  string help = "type of noise" ]],

    int outdim = 3
        [[  string help = "dimensions of output (1-4)",
            int min = 1, int max = 3 ]],
    output color Cout = 0
        [[  string help = "color output (if outdim > 1)"  ]],
    output float fout = 0
        [[  string help = "float output (if outdim == 1)"  ]]
  )
{
    if (outdim == 1) {
        float fnoise = (float) noise (noisetype,
                                      position.x*scale, position.y*scale);
        fout = pivot + amplitude * fnoise;
    } else {
        color cnoise = (color) noise (noisetype,
                                      position.x*scale, position.y*scale);
        Cout = pivot + amplitude * cnoise;
        fout = Cout[0];
    }
}

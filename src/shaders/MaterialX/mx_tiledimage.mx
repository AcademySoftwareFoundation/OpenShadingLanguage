// Copyright Contributors to the Open Shading Language project.
// SPDX-License-Identifier: BSD-3-Clause
// https://github.com/AcademySoftwareFoundation/OpenShadingLanguage
//
// MaterialX specification (c) 2017 Lucasfilm Ltd.
// http://www.materialx.org/

#include "mx_funcs.h"


shader mx_tiledimage_TYPE_SUFFIX
    [[ string help = "tiledimage node -- Texture an image, tiled" ]]
  (
    string file = ""
        [[  string help = "Texture filename",
            string widget = "filename" ]],
    string layer = ""
        [[  string help = "Subimage in texture file",
            string widget = "filename" ]],
    TYPE default_value = TYPE_ZERO
        [[  string help = "Default value" ]],
    vector2 texcoord = {u, v}
        [[ string help = "Vector2 texture coordinates",
           int lockgeom = 0 ]],
    vector2 uvtiling = { 1.0, 1.0 }
        [[ string help = "Tiling rate" ]],
    vector2 uvoffset = { 0.0, 0.0 }
        [[ string help = "Tiling offset" ]],
    string filtertype = "smartcubic",

    output TYPE out = TYPE_ZERO
        [[  string help = "Output color" ]]
  )
{
    color missingColor;
    float missingAlpha;
    setup_missing_color_alpha (default_value, missingColor, missingAlpha);

    vector2 st = (texcoord / uvtiling) - uvoffset;
    float alpha;
    color rgb = texture (file, st.x, st.y,
                         "alpha", alpha, "subimage", layer,
                         "wrap", "periodic",
                         "interp", filtertype,
                         "missingcolor", missingColor,
                         "missingalpha", missingAlpha);

    out = combine (rgb, alpha);
}

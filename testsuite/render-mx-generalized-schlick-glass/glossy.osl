// Copyright Contributors to the Open Shading Language project.
// SPDX-License-Identifier: BSD-3-Clause
// https://github.com/AcademySoftwareFoundation/OpenShadingLanguage


surface
glossy
    [[ string description = "Glossy glass material" ]]
(
    color Cr = 1.0
        [[  string description = "Reflectance tint",
            float UImin = 0, float UImax = 1 ]],
    color Ct = 1.0
        [[  string description = "Transmission tint",
            float UImin = 0, float UImax = 1 ]],
    float ior = 1.5
        [[  string description = "Index of refraction",
            float UImin = 1.0, float UImax = 3.0 ]],
    float roughness = 0.5
        [[  string description = "Roughness of the surface",
            float UImin = 0, float UImax = 1 ]],
    float anisotropy = 0.0
        [[  string description = "Anisotropy of the surface",
            float UImin = 0, float UImax = 1 ]]
  )
{
    float alpha = roughness * roughness;
    float alpha_x = alpha * (1.0 - anisotropy);
    float alpha_y = alpha;
    vector U = normalize(cross(N, dPdv));
    float f0 = pow((ior - 1) / (ior + 1), 2);
    Ci = generalized_schlick_bsdf(N, U, Cr, Ct, alpha_x, alpha_y, color(f0), color(1.0), 5.0, "ggx") + medium_vdf(color(0), 1.0, color(0), 0.0, ior, 0);
}
